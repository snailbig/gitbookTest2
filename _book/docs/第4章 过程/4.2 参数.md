##4.2 参数
在Nim中有两种类型的参数，一种是在过程体内不允许改变其值的参数，另一种是允许改变其值的参数。第一种不允许改变该值是为了是编译器能够以最高效的方式传递参数，例如前面我们写过得例子：

    proc sum(a, b: int): int = 
      result = a + b
    
    echo sum(1,2)
    
在sum过程中不允许改变a或者b的值，如果添加语句a = a+1，这样在程序编译时会出现错误，提示信息： Error: 'a' cannot be assigned to。

那如果我们想要在过程体中改变参数怎么办呢？这就要讲到第二种参数了，要想在过程体中改变参数的值，可以在函数定义的时候在类型前添加关键字var。例如：

    proc sum(a, b: var int): int = 
      a = a + 1
      result = a + b
    
    var a, b = 1
    echo sum(a,b) #echo 3
    echo a  #echo 2
   
这样就可以改变参数a和b的值了，并且这样改变了a的真实值，在sum过程外，a的值也随之而变。

那我们来再看一个例子，看看为什么在过程中改变参数的值，过程结束后实参的值也随之而变。

    proc divmod(a, b: int; res, remainder: var int) =
      res = a div b
      remainder = a mod b
    var
      x, y: int
    divmod(8, 5, x, y) # modifies x and y
    assert x == 1
    assert y == 3

在上面的例子中，a,b是不带 var 的参数，它们在过程中是不允许改变的，而 res, remainder 在过程中是能够改变其值的。 这里需要注意的是：传递给 var 参数的必须是一个 l-value.
    
l-value 介绍：
 一个表达式能指定一个产生一个值或位置的计算。产生位置的表达式就是 l-values。（比如：var x = 5 中的 x 就是一个l-value。 5 就不是）。一个 l-value 能够表示一个位置或者这个位置包含的值，这取决于语境。表达式的值能够被静态确定的被称为常量表达式。它们不是 l-values。let 声明的变量不是 l-values。

其实var 参数是作为隐式的指针实现的，上面的例子与这个例子是等价的：

    proc divmod(a, b: int; res, remainder: ptr int) =
      res[] = a div b
      remainder[] = a mod b
    
    var
      x, y: int
    divmod(8, 5, addr(x), addr(y))
    assert x == 1
    assert y == 3

上面的例子中，ptr表示的是指针类型，调用过程divmod时addr(x)和addr(y)表示的是x和y的地址。由此可以看出以var类型参数传递的是地址，通过指针来改变实参的值。


###多种类型参数表示

在定义一个过程时，很可能有多种类型的参数，那么我们怎么去表示呢。
    
（1）使用逗号分割 

    proc strAdd(str1: var string, str2: string, n: bool) = 
      if n:
        str1 = str1 & str2
    
    var 
      str1 = "yr"
    strAdd(str1,"ss",true)
    echo str1         #echo  yrss

（2）使用分号分割

    proc strAdd(str1: var string; str2: string; n: bool) = 
      if n:
        str1 = str1 & str2
    
    var 
      str1 = "yr"
    strAdd(str1,"ss",true)
    echo str1

当然你也可以混合使用，但是这样可读性会大大降低。


# 任真
***
## 4.2 参数
在程序体中参数是恒定的。默认情况下，它们的值不能改变因为这允许编译器以一种更有效的方式实现参数传递。如果在过程中需要一个易变的变量，它必须在过程体中用var声明。声明的变量可以与参数有相同的名字，事实上这是一个惯用语法。
```
type 
   SType=seq[int]
var 
  sVar:SType= @[1,2,3]
  nprinted:int = -1

proc printSeq(s: seq, nprinted: int = -1) =
  var nprinted = if nprinted == -1: s.len else: min(nprinted, s.len)
  for i in 0 .. <nprinted:
    echo s[i]


sVar.printSeq(nprinted) 
```
如果程序需要为调用者修改实参，可以使用var参数:(我自己理解这句话的意思是：就像其他语言，值调用是不会改变实参的值，要想改变实参的值要通过引用调用，nim中是在过程中将参数用var声明就可以实现调用参数的改变)(后注:在nim中如果实参不用var声明，在过程中是不能改变其值的)
```
proc divmod(a, b: int; res, remainder: var int) =
  #在nim中如果实参不用var修饰，则在过程中无法改变其值
  res = a div b        # 整数除法
  remainder = a mod b  # 整数模操作

var
  x, y: int
divmod(8, 5, x, y) # 将会修改x和y的值
echo(x)
echo(y)
```
在例子中，res和remainder是var参数。var参数可以通过过程修改，然后改变化将传递给调用者。注意：上面的例子使用元组作为返回值而不是使用var参数将会更好。
```
proc divmod(a,b:int):tuple[res:int,remainder:int]=
  result.res=a div b
  result.remainder= a mod b
echo divmod(8,5)
```
### 4.2.1 var 参数
带有var关键字前缀的参数类型:
```
proc divmod(a, b: int; res, remainder: var int) =
  res = a div b
  remainder = a mod b

var
  x, y: int

divmod(8, 5, x, y) # modifies x and y
assert x == 1
assert y == 3
```
在这个例子中，res和remainder是var参数.var参数被程序修改并且对于调用者该改变是可见的.传递给var参数的实参必须是一个l-value(左值).var参数作为隐藏指针实现。上面的例子等价于:
```
proc divmod(a, b: int; res, remainder: ptr int) =
  res[] = a div b
  remainder[] = a mod b

var
  x, y: int
divmod(8, 5, addr(x), addr(y))
assert x == 1
assert y == 3
```
在上面的例子中，var参数或指针用于提供两个返回值。这可以通过返回一个元组以一种更简洁的方式实现:
```
proc divmod(a, b: int): tuple[res, remainder: int] =
  (a div b, a mod b)

var t = divmod(8, 5)

assert t.res == 1
assert t.remainder == 3
```
可以使用元组拆包去访问元组的字段域:
```
proc divmod(a, b: int): tuple[res, remainder: int] =
  (a div b, a mod b)

var (x, y) = divmod(8, 5) # tuple unpacking

assert x == 1
assert y == 3
```
注意:对于有效的参数传递var参数是没有必要的.由于非var参数不能被修改，如果它认为它可以加快执行速度编译器总是自由的通过引用传递参数.
### 4.2.2 var return type
一个过程，转换器或者迭代器可能返回一个var类型，它意味着返回的值是一个l-value(左值)并可以被调用者修改:
```
var g = 0

proc WriteAccessToG(): var int =
  result = g

assert WriteAccessToG()==0
g=7
assert WriteAccessToG()==7

WriteAccessToG() = 6
assert g == 6
```
这是一个编译时错误如果隐式引入的指针可以访问一个超出它生命周期的位置:
```
proc WriteAccessToG(): var int =
  var g = 0
  result = g    #Error: address of 'g' may not escape its stack frame
```
对于迭代器，元组返回类型的一部分也可以有一个var类型:
```
type
  SType=seq[string]

var sValue:SType= @["east","west","north","south"]

iterator mpairs(a: var seq[string]): tuple[key: int, val: var string] =
  #返回的元组的val域有var修饰，作用是什么呢?
  for i in 0..a.high:
    yield (i, a[i])


for i,value in mpairs(sValue):
  echo i," : ",value

sValue= @["east","west","north","southern"]

for i,value in mpairs(sValue):
  echo i," : ",value
```
在标准库中每一个返回var类型的程序的名字都以m前缀开始。

