##4.6 过程重载
Nim提供了与C++相似的过程重载。

    proc toString(x: int): string = 
      if x != 0:
        result = $x 
      else:
        result = "zero"
    proc toString(x: bool): string =
      if x: result = "true"
      else: result = "false"
    
    echo(toString(13))   # calls the toString(x: int) proc
    echo(toString(true)) # calls the toString(x: bool) proc

上面程序中，声明了两个过程名相同的过程，但是参数类型是不一样的，从而实现了过程重载。当像过程toString传递13时，编译器会根据实参类型去选择相应的过程。
     
对于运算符我们也可以重载，在下面的程序里我们实现了对运算符`+`的重载。

    proc `+`(a, b: string): string =
      result = a & b
    
    var
      str1 = "abc"
      str2 = "yrs"
    echo str1+str2

# 任真
***
## 4.6  过程重载
***
nim提供了与c++相似的重载过程的功能：
```
proc toString(x: int): string = 
  result= $(x)
proc toString(x: bool): string =
  if x: result = "true"
  else: result = "false"

echo(toString(13))   # calls the toString(x: int) proc
echo(toString(true)) # calls the toString(x: bool) proc
```
上面的例子中，toString()过程除了参数类型不同外，其他都相同，实现了重载。
（注意在nim中toString通常是$操作符）对于toString的调用编译器会选择最合适的过程。这个重载解析算法具体是怎样工作的不在这里讨论（它将在手册中详述）。然而，它不会引起糟糕的意外，它是基于一个非常简单的统一算法。模糊不清的调用将会报错。

在这里我们仅介绍Nim中过程重载，在后面的介绍中我们会介绍到更多Nim中的重载，如:操作符重载.
### 4.6.1 重载决议
在一个p(args)调用中，将选择匹配最好的p程序.如果有多个程序都能很好的匹配，则在编译时会报告歧义.
参数中的每一个参数都需要匹配。一个参数如何匹配有多种不同的匹配策略.假设f为形式参数的类型，a为实参类型.
* 精确匹配:a和f是相同的类型
* 字面量匹配:a是一个整形字面量值v,f是一个有符号或者无符号整型，并且v在f的范围中.或者:a是一个浮点字面量值v，f是一个浮点类型，并且v在f的范围内.
* 泛型匹配:f是一个泛型类型然后a匹配，例如a是int,f是泛型参数类型(就如[T]或者[T:int|char])
* 子界或者子类型匹配:a是一个range[T],T正好匹配f.或者:a是f的一个子类型.
* 整体转换匹配:a转换到f，f和a是整型或者浮点指针类型.
* 转换匹配:a转换到f，可能通过用户自定义的转换器.

这些匹配策略有优先级:精确匹配高于字面量匹配，字面量匹配高于泛型匹配等.下列count(p,m)计数对于程序p匹配策略匹配的数量。
如果下列算法返回true,程序p比程序q能更好匹配:
```
for each matching category m in ["exact match", "literal match",
                                "generic match", "subtype match",
                                "integral match", "conversion match"]:
  if count(p, m) > count(q, m): return true
  elif count(p, m) == count(q, m):
    discard "continue with next category m"
  else:
    return false
return "ambiguous"
```
一些例子:
```
proc takesInt(x: int) = echo "int"
proc takesInt[T](x: T) = echo "T"
proc takesInt(x: int16) = echo "int16"

takesInt(4) # "int"

var x: int32
takesInt(x) # "T"

var y: int16
takesInt(y) # "int16"

var z: range[0..4] = 0
takesInt(z) # "T"
```
如果这个算法返回"ambiguous",将执行进一步消岐:通过一个子匹配关系，如果实参a同时匹配形参类型f的p和g的q，将考虑继承:
```
type
  A = object of RootObj
  B = object of A
  C = object of B

proc p(obj: A) =
  echo "A"

proc p(obj: B) =
  echo "B"

var c = C()
# not ambiguous, calls 'B', not 'A' since B is a subtype of A   没有歧义，调用'B',而不是'A',因为B是Ａ的子类型,但是反之却不成立:
# but not vice versa:
p(c)

proc pp(obj: A, obj2: B) = echo "A B"
proc pp(obj: B, obj2: A) = echo "B A"
#proc pp(obj:B,obj2:C)=echo "B C"　　　　　如果加上这个过程则没有问题
# but this is ambiguous:　　　但是这样是歧义的
pp(c, c)
```
同样对于泛型匹配，执行最专业的泛型匹配:
```
proc gen[T](x: ref ref T) = echo "ref ref T"
proc gen[T](x: ref T) = echo "ref T"
proc gen[T](x: T) = echo "T"

var ri: ref int
gen(ri) # "ref T"

var rri:ref ref int
gen(rri)

var r:int
gen(r)
```
如果形式参数f是var T 类型，除了序数类型检查，实参检查为一个l-value(左值).var T比Ｔ匹配更好.
```
proc sayHi(x: int): string =
  # matches a non-var int
  result = $x


proc sayHi(x: var int): string =
  # matches a var int
  result = $(x + 10)

proc sayHello(x: int) =
  var m = x 　　　　　　# 一个易变的版本x
  echo sayHi(x) 　　# 匹配non-var版本的sayHi
  echo sayHi(m) 　　#  匹配var版本的sayHi


sayHello(3) # 3
            # 13
```
