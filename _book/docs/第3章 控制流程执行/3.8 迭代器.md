##3.8 迭代器
让我们回到这个无聊的计数的例子：

    echo("Counting to ten: ")
    for i in countup(1, 10):
      echo($i)

写一个countup过程能不能支持这个循环呢？让我们试一下：

    proc countup(a, b: int): int =
      var res = a
      while res <= b:
        return res
        inc(res)

然而这个是不能工作的，问题是过程不仅仅只有返回，而是既需要返回，也需要在一次迭代完后继续下一次迭代执行。很明显这个程序中，while语句第一次遇到return后该过程就结束了。要想实现就有返回，又有能继续迭代，就需要用到yield语句。现在就让我们使用关键字iterator代替proc，yield语句代替return语句来实现我们的第一个迭代器：

    iterator countup(a, b: int): int =
      var res = a
      while res <= b:
        yield res
        inc(res)

迭代器看起来和过程非常相似，但是它们之间有一些重要的区别：


1. 迭代器仅仅能够for循环调用


1. 迭代器不能包含return语句，过程不能包含yield语句


1. 迭代器没有隐含result变量


1. 迭代器不支持递归


1. 迭代器不能被前置声明，因为编译器必须能够内联一个迭代器。（在未来版本的编译器中这个限制将会消失。
  
有时候迭代器也是可以省略的

    # this definition exists in the system module
    iterator items*(a: string): char {.inline.} =
      var i = 0
      while i < len(a):
        yield a[i]
        inc(i)
    
    for ch in items("hello world"): # `ch` is an iteration variable
      echo(ch)

这个items迭代器在system模块中有定义。它的参数也有不同的类型，它的功能是返回参数（数组，字符串，序列等等）的单元量。你也可以省略items，直接写一个参数，这样编译器编译时会添加items。

例如上面的程序：

    for ch in items("hello world"): # `ch` is an iteration variable
      echo(ch)

可以改写为：

    for ch in "hello world": # `ch` is an iteration variable
      echo(ch)

再例如：

    for x in [1,2,3]:
      echo x
  
会被编译器改写为：

    for x in items([1,2,3]):
      echo x

  
如果for循环有两个变量，那么会隐式的调用pairs迭代器。

    for i, value in [2,4,6]:
      echo "index: ", i, " value: ", value

同样相当于：

    for i, value in pairs([2,4,6]):
      echo "index: ", i, " value: ", value

Nim不仅使用迭代器方便，而且还可以很容易的自己定义一个迭代器，请看下面这段代码：

    iterator mySquare(a, b: int): int = 
      var res = a
      while res <= b:
        yield res*res
        inc(res)
    
    for i in mySquare(1,5):
      echo i

自己定义一个运算符迭代器：

    iterator `...`*[T](a: T, b: T): T = 
      var res: T = T(a)
      while res <= b:
        yield res
        inc res

    for i in 0...5:
      echo i


#任真
***
## 3.8 迭代器
***
### 3.8.1 迭代器和for语句
&nbsp;&nbsp;&nbsp;&nbsp;for语句是一个抽象的机制来迭代一个容器中的元素。它的实现依赖于迭代器。如while语句，for语句打开一个隐式的块，所以可以使用break语句跳出for循环.

&nbsp;&nbsp;&nbsp;&nbsp;for循环声明迭代变量－迭代变量的作用域直到循环体结束为止。迭代变量的类型由迭代器的返回类型推断决定。

&nbsp;&nbsp;&nbsp;&nbsp;一个迭代器与一个过程类似，除了它可以在一个for循环的上下文中调用。迭代器提供一种方法来指定迭代一个抽象类型。在一个for循环执行中一个关键角色承担被调用迭代器中的yield语句。无论何时一个yield语句达到for循环变量的绑定值然后控制在for循环体中继续。在调用中自动保存迭代器的本地变量和执行状态。

```
# this definition exists in the system module
iterator items*(a: string): char {.inline.} =
  var i = 0
  while i < len(a):
    yield a[i]
    inc(i)

for ch in items("hello world"): # `ch` is an iteration variable
  echo(ch)
```
编译器生成代码，犹如程序员将代码写成这样:
```
var i = 0
while i < len(a):
  var ch = a[i]
  echo(ch)
  inc(i)
```
注:这里我们会对迭代器中的yield语句感到很陌生，我们简单介绍一下yield语句。在迭代器中使用yield语句而不是return语句。yield仅在迭代器中有效.执行返回到for循环体叫做迭代器。yield不是结束迭代过程，而是如果下一次迭代开始，执行将传递回迭代器。

### 3.8.2 隐式的items/pairs调用
在介绍for语句时，我们介绍到了这方面的内容，我们再来重新学习一下:如果for循环表达式e并不代表一个迭代器，并且for循环正好仅有１个变量，则for循环表达式被重写为items(e);隐式的调用items迭代器:
```
for x in [1,2,3]: echo x
```
如果for循环正好有2个变量，隐式的调用pairs迭代器.

在重载步骤之后，将会执行items/pairs标识符的符号检查，所以将会考虑所有重载的items/pairs迭代器。

### 3.8.3 迭代器与过程
让我们回到无聊的计数实例：
```
echo("Counting to ten: ")
for i in countup(1, 10):
  echo($i)
```
可以写一个conutup过程支持这个循环吗？让我们试试
```
proc countup(a, b: int): int =
  var res = a
  while res <= b:
    return res
    inc(res)
```
然而，这不工作。问题是这个过程应该不仅需要return，而是需要返回和在一次迭代完成之后continue。这个返回和继续称为一个yield语句。现在剩下的唯一要做的事情是用iterator替换proc关键字，这是我们的第一个迭代器：
```
iterator countup(a, b: int): int =
  var res = a
  while res <= b:
    yield res
    inc(res)
```
迭代器看起来与过程很像。但它们之间存在几点很大的不同：
* 迭代器只能从for循环调用
* 迭代器不能包含return语句，过程不能包含yield语句。
* 迭代器没有隐含的result变量
* 迭代器不支持递归
迭代器不能提前声明，因为编译器必须能够内联一个迭代器（这个限制将会在未来版本的编译器中消失）

然而，你也可以使用一个封闭的迭代器来获得一组不同的限制。详情见第一类迭代器。迭代器可以与一个过程有相同的名字和参数，本质上它们有自己的命名空间。因此它常见的做法是使用相同名字的过程过程代替迭代器，积累迭代器的结果，并将它们作为一个序列返回.

### 3.8.4 高级迭代器
在Nim中有2类迭代器:inline和closure迭代器。一个内联迭代器是一个由编译器内联的迭代器将会导致零开销的抽象，但是可能导致在代码大小上一个巨大的增加。内联迭代器是第二等公民;它们仅可以作为参数传递给其他内联工具，如模块，宏和其他内联迭代器.

与内联迭代器相反的是，可以更自由的传递闭包迭代器:
```
iterator count0(): int {.closure.} =
  yield 0

iterator count2(): int {.closure.} =
  var x = 1
  yield x
  inc x
  yield x

proc invoke(iter: iterator(): int {.closure.}) =
  for x in iter(): echo x

invoke(count0)
invoke(count2)
```
相比内联迭代器，闭包迭代器有其他的限制:
* 闭包迭代器中的yield不能出现在一个try语句中。
* 目前，一个闭包迭代器无法在编译时评估。
* 在一个闭包迭代器中允许return语句(但是很少使用),然后结束迭代.
* 无论是内联迭代器还是闭包迭代器都可以循环.
没有显式的标记{.closure.}或者{.inline.}的迭代器默认为内联迭代器，但是在未来版本的实现中这可能会改变。

迭代器类型总是隐式的调用closure约定，下面的例子展示怎样使用迭代器来实现一个协同的任务系统:
```
# simple tasking:
type
  Task = iterator (ticker: int)

iterator a1(ticker: int) {.closure.} =
  echo "a1: A"
  yield
  echo "a1: B"
  yield
  echo "a1: C"
  yield
  echo "a1: D"

iterator a2(ticker: int) {.closure.} =
  echo "a2: A"
  yield
  echo "a2: B"
  yield
  echo "a2: C"

proc runTasks(t: varargs[Task]) =
  var ticker = 0
  while true:
    let x = t[ticker mod t.len]
    if finished(x): break
    x(ticker)
    inc ticker

runTasks(a1, a2)
```
内置的system.finished可以用于判断是否一个迭代器已经完成了它的操作，在试图调用一个已经完成它的工作的迭代器时不会引发异常。

注意system.finished容易错误使用，因为在迭代器完成一次迭代后它仅返回true:
```
iterator mycount(a, b: int): int {.closure.} =
  var x = a
  while x <= b:
    yield x
    inc x

var c = mycount # instantiate the iterator
while not finished(c):
  echo c(1, 3)
```
而不是这个已经使用过的代码:
```
var c = mycount # instantiate the iterator
while true:
  let value = c(1, 3)
  if finished(c): break # and discard 'value'!
  echo value
```
它有助于认为迭代器实际返回pair (value, done)并且finished用于访问隐藏的done字段.

闭包迭代器是可恢复的功能，所以必须给每一次调用都提供参数。为了解决这个限制一个外部过程可以捕获参数:
```
proc mycount(a, b: int): iterator (): int =
  result = iterator (): int =
    var x = a
    while x <= b:
      yield x
      inc x

let foo = mycount(1, 4)

for f in foo():
  echo f
```
Nim有高级迭代器并且有语法去使用它们,for循环.continue和break关键字也作用在for循环内部.这有两种类型的迭代器，和两个特殊的for循环使用方法.

### 3.8.5 items and pair
当迭代有一项的对象时，Nim调用items迭代器，它的第一个参数是你想迭代的元素类型。当调用有两项的对象时，同理，但是在这种情况下，将调用pairs迭代器.
```
type
  TRange = object
    low: int
    high: int

iterator items(range: TRange): int =
  var i = range.low
  while i <= range.high:
    yield i
    inc i

iterator pairs(range: TRange): tuple[a: int, b: char] =
  for i in range:  # uses Range.items
    yield (i, char(i + ord('a')))

for i, c in TRange(low: 1, high: 3):
  echo c
```
### 3.8.6 Operators
迭代器也可以成为标准方式的操作符，使用单反号(``)将名字扩起来。例如，标准库定义slice迭代器，允许它迭代序数类型.
```
# Give it a different name to avoid conflict
iterator `...`*[T](a: T, b: T): T =
  var res: T = T(a)
  while res <= b:
    yield res
    inc res

for i in 0...5:
  echo i
```
### 3.8.7 Inline Iterators
内联迭代器基本在for循环主体中然后将它内联到迭代器中。这意味着它们没有任何函数调用开销，但是如果不小心创建可能会大幅度增加代码大小.
```
iterator countTo(n: int): int =
  var i = 0
  while i <= n:
    yield i
    inc i

for i in countTo(5):
  echo i
```
### 3.8.8 Closure Iterators
闭包迭代器保持它们自己的状态，可以在任何时候继续。finished()函数可用来检查在迭代器中是否还有可用的元素，然而原生迭代器的用法是不直观的并且很容易出错。
```
proc countTo(n: int): iterator(): int =
  return iterator(): int =
    var i = 0
    while i <= n:
      yield i
      inc i

let countTo20 = countTo(20)

echo countTo20()

var output = ""
# Raw iterator usage:
while true:
  # 1. grab an element
  let next = countTo20()
  # 2. Is the element bogus? Its the end of the loop, discard it
  if finished(countTo20):
    break
  # 3. Loop body goes here:
  output.add($next & " ")

echo output

output = ""
let countTo9 = countTo(9)
for i in countTo9():
  output.add($i)
echo output
```
