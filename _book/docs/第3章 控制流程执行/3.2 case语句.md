## 3.2 case语句
***
提供分支的另一种方法是用case语句，case语句与if语句是相似的，但是它提供的是多分支选择结构。首先关键字case后面的表达式被评估，如果它的值是在关键字of后面的表达式里，那么就执行这个of分支对应的代码。如果这个值不属于任何一个给定的of列表分支，那么else语句部分将被执行。如果没有else部分，并且expr可能出现的所有值并不都在一个of分支列表中，将会发生一个静态错误。这个只适用于序数类型的表达式。expr所有可能的值通过expr的类型来判定。为了解决该静态类型错误，可以使用一个带有空discard语句的else部分。
```
let name = readLine(stdin)
case name
of "":
  echo("Poor soul, you lost your name?")
of "name":
  echo("Very funny, your name is name.")
of "Dave", "Frank":
  echo("Cool name!")
else:
  echo("Hi, ", name, "!")
```
正如你所见，一个of分支的值可以有多个，它们之间用逗号分隔开。

Case语句能够处理整型、其他序数类型和字符串。（关于什么是序数类型之后的章节将会解释）对于of后面的值也可以是整型值范围或其他序数类型值的范围：
```
# this statement will be explained later:
from strutils import parseInt

echo("A number please: ")
let n = parseInt(readLine(stdin))
case n
of 0..2, 4..7: echo("The number is in the set: {0, 1, 2, 4, 5, 6, 7}")
of 3, 8: echo("The number is 3 or 8")
```
上面的代码中第一句from...import...语句，就像Python或Java中的import导入其他文件的函数一样，这里是导入strutils模块中的parseInt过程。

然而，上面的代码是不能编译通过的，原因是你必须列举覆盖n可能包含的所有值，但是这段代码只处理了0..8的值。由于列出所有其他可能的整型是不切实际的，我们可以通过告诉编译器对于剩余的其他值没有任何事要做，或都执行相同的代码块的方式来处理这个问题。
```
...
case n
of 0..2, 4..7: echo("The number is in the set: {0, 1, 2, 4, 5, 6, 7}")
of 3, 8: echo("The number is 3 or 8")
else: discard
```
你也可以把else语句改为：
```
else: echo("others")
```
discard是Nim中的关键字，意味着编译器什么都不做。这个空discard语句是什么也不做的声明。关于discard更详细的介绍在后面的章节中会讲到。编译器知道case语句中带有else部分就包括了所有情况，所以错误消失。这里的else就像if语句中的else，当前面条件都不满足时，就执行else语句里的内容。注意，想要覆盖所有的字符串是不可能的，这就是为什么处理字符串的case语句总是带有else分支的原因。

一般来说，case语句用于子界类型或枚举类型是非常有用的，编译器能够检查你覆盖的任何可能的值。

子界类型：
```
type
  octalRange = range[0..7]

var x:octalRange
x = 3

case x
of 0..3:
  echo "x in range 0..3"
of 4..7:
  echo "x in range 4..7"
```
上面代码中type代码块里是定义了一个类型octalRange，它是子界类型，其取值范围是0..7，现在只是了解一下，更详细的内容后面章节会讲到。

枚举类型：
```
type
  RGB = enum
    red, green, blue

var color: RGB
color = green
case color
of red:
  echo "red"
of green:
  echo "green"
of blue:
  echo "blue"
```
上面的代码中，type代码块中同样是定义了一个类型RGB，它是枚举类型。这两个程序都列出了所有可能值，所以不用使用else部分了。

Case语句中的缩进和冒号都是可选的:
```
＃不使用冒号和不缩进
case readline(stdin)
of "delete-everything", "restart-computer":
  echo("permission denied")
of "go-for-a-walk":     echo("please yourself")
else:                   echo("unknown command")

#使用冒号和不缩进
case readline(stdin):
of "delete-everything", "restart-computer":
    echo("permission denied")
of "go-for-a-walk":     echo("please yourself")
else:                   echo("unknown command")  
```

当然你也可以不加冒号进行缩进，加冒号进行缩进，例:
```
# 使用冒号加缩进
case readline(stdin):
  of "delete-everything", "restart-computer":
    echo("permission denied")
  of "go-for-a-walk":     echo("please yourself")
  else:                   echo("unknown command")

#不适用冒号加缩进
case readline(stdin)
  of "delete-everything", "restart-computer":
    echo("permission denied")
  of "go-for-a-walk":     echo("please yourself")
  else:                   echo("unknown command")
```
可见Nim的代码风格给了程序员最大的自由.

作为一种特殊的语义扩展，case语句的of分支可以对一个集合或数组构造器进行评估，这个集合或数组的元素会在一个列表中展开。
```
const
  SymChars: set[char] = {'a'..'z', 'A'..'Z', '\x80'..'\xFF'}

proc classify(s: string) =
  case s[0]
  of SymChars, '_': echo "an identifier"
  of '0'..'9': echo "a number"
  else: echo "other"

# is equivalent to:
proc classify2(s: string) =
  case s[0]
  of 'a'..'z', 'A'..'Z', '\x80'..'\xFF', '_': echo "an identifier"
  of '0'..'9': echo "a number"
  else: echo "other"
```
上面这段程序中，开头的const语句声明了一个常量SymChars，它的类型是字符集合set[char]，值为集合{'a'..'z', 'A'..'Z', '\x80'..'\xFF'}。上面的两个过程classify和classify2是等价的。


