##6.6 序列

序列和数组是相似的，但是序列的长度在运行时是可以动态改变的（像字符串）。序列是作为可变长度数组来实现的，随着元素被添加分配新的内存。一个序列S总是通过整数索引，其范围是0到len(S)-1，并且是有边界检查的。由于序列是可调整大小的，所以他们总是被分配在堆上，并且可以垃圾回收。

我们可以通过数组构造器 [] 来构造序列，通过运算符@把数组转化为序列。其他的方式是通过内建的newSeq过程来分配空间给一个序列。

    var
      x: seq[int] # a reference to a sequence of integers
    x = @[1, 2, 3, 4, 5, 6] # the @ turns the array into a sequence allocated on the heap
    echo x

#
    var inputStrings : seq[string]
    newSeq(inputStrings, 3)
    inputStrings[0] = "The fourth"       #创建一个序列后，要用赋值语句，而不是add
    inputStrings[1] = "assignment"
    inputStrings[2] = "would crash"
    #inputStrings[3] = "out of bounds"    #error
    echo inputStrings

#
    var inputStrings = newSeq[string](3)
    inputStrings[0] = "The fourth"
    inputStrings[1] = "assignment"
    inputStrings[2] = "would crash"
    #inputStrings[3] = "out of bounds"
    echo inputStrings

#
一个序列可能被传递给一个openarray参数。

    proc seqAdd(x: openArray[int]): int = 
      for i in x:
        result = result + i
    
    var x: seq[int] = @[1, 2, 3, 4]
    echo seqAdd(x)

序列变量默认的初始化为nil。然而大多数的序列操作考虑到性能原因不能处理nil（会引起一个异常），因此应该使用一个空序列@[]，而不是nil作为空值。但是@[] 会在堆上创建一个序列对象，因此这里会有一个权衡。

对于for语句在使用一个序列时可以用一个变量或是两个变量，当你使用一个变量时，这个变量将会是序列提供的元素，For语句会使用system模块中的items()迭代器去迭代序列。但是如果你使用两个变量，那么第一个变量将会是位置的索引，第二个变量会是序列的值，这个时候for语句将会使用pairs()迭代器。例如：

    for i in @[3, 4, 5]:
      echo($i)
    # --> 3
    # --> 4
    # --> 5
    
    for i, value in @[3, 4, 5]:
      echo("index: ", $i, ", value:", $value)
    # --> index: 0, value:3
    # --> index: 1, value:4
    # --> index: 2, value:5


序列索引总是以一个int型0位置开始，len、low和high操作能够用于序列类型，符号x[i] 是访问序列x的第i个元素。序列的边界可以通过内建的过程low()和high()得到，它们分别返回最小索引和最大索引。我们可以通过过程add()或 & 来向一个序列添加元素，可以通过过程pop()来移除（和得到）这个序列的最后一个元素。

    var 
      x: seq[int]
    x = @[1,2,3,4,5]
    echo x
    
    x.add(6)  
    echo x          #add过程添加元素到序列x
    
    echo x & 7      # &只是返回一个连接后的序列，而x并没有改变
    echo x
    
#




    var 
      x: seq[int]
    x = @[1,2,3,4,5]
    echo x
    
    echo x.pop()        #最后一个元素被移除
    echo x

#
    var
      a = @[1, 2, 3]
      b = newSeq[int](3)
    
    for i, v in a:
      b[i] = v*v
    
    for i in 4..100:
      b.add(i * i)
    
    b.delete(0)  # takes O(n) time      #删除第0个元素
    b = a[0] & b  # Same as original b

#

    type
      TMsg = tuple[
        file: string,
        line: int,
        msg: string,
        err: bool]
    
    var s: seq[TMsg] = @[]
    
    s.add(("fA", 13, "msg1", false))
    s.add(("fB", 14, "msg2", true))
    s.add(("fC", 15, "msg3", false))
    
    for file, line, msg, err in items(s):
      stdout.write(file)
      stdout.write($line)
      stdout.write(msg)
      stdout.write($err)
    
    #OUT fA13msg1falsefB14msg2truefC15msg3false

# 任真
***
## 6.6 序列
***
序列类似与数组，但它有可以在运行时改变的动态长度（就像字符串）。由于序列是可变大小的，所以它们总是在堆上分配以及垃圾收集。序列被实现为动态增长的数组，分配内存片作为它的项添加。一个序列S通常通过从0到len(S)-1的整数值索引，并且它有边界检查。

序列可以通过数组构造器[]结合数组到序列操作符@来构造。另一种为序列分配空间的方法是调用内置的newSeq过程。
```
var
  x: seq[int] #  一个整型序列
  y:seq[char]
x = @[1, 2, 3, 4, 5, 6] # @操作将数组转换为序列

y=newSeq[char](3)   #使用内置的newSeq过程为序列分配空间
#is equal to
y=newSeq[char]()     #也可以不带分配的大小，两者是等价的

#注意: 如果你把初始化序列写成这样x =@[1, 2, 3, 4, 5, 6]就会出现Error: undeclared identifier: '=@'
```
一个序列可以传递给一个开放数组参数.这在下面介绍开放数组时会介绍.

一个数组或者序列的下界可以通过内置的low()过程取得，通过high()取得上界。长度可以通过len()取得。序列总是使用从位置0开始的int类型索引。low()对于一个序列或者一个开放数组总是返回0，因为这是第一个有效的下标值。你可以使用add()过程或者&操作符实现给一个序列添加元素，使用pop()过程移除（然后得到）一个序列的最后一个元素。

x[i]符号可以用于访问x的第i个元素。
```
var
  x: seq[int]                 # a sequence of integers
x = @[1, 2, 3, 4, 5, 6]       # the @ turns the array into a sequence


echo x.low     #最小下标值，通常为0
echo x.high    #最大下标值

x.add(@[7])
echo x
x.add(8)
echo x

echo x.pop   #移出并得到
echo x

echo x & 9     #&操作符只是在序列后附加了一个元素，序列本身并没有变化
x=x & 0        
echo x
echo x.high

echo x.pop
echo x
```
序列变量用nil初始化。然而，由于性能的原因很多的序列操作不能处理nil（导致引发一个异常）。因此应该用空序列@[]而不是nil作为空值。但是@[]在堆上创建了一个序列对象，所以在这里需要做一个权衡.
```
var
  sValue:seq[int]

echo sValue

echo len(sValue) 
#sValue.add(2)            #出现错误
sValue= @[]
sValue.add(2)             #当我们将序列用@[]初始化之后,再进行操作就没有问题了
echo sValue
```
在nim中迭代序列有三种基本方法:
* 通过序列项迭代
```
var dirList:seq[string]
dirList= @["east", "west", "south", "north"]

for eachDir in dirList:
  echo eachDir
```
在上面的例子中, 我们迭代一个列表. 每次迭代, eachDir 变量都被设置为列表中特定某个元素,然后我们在代码块中打印出这个变量.
* 通过序列索引迭代
另个方法就是通过序列的索引来迭代:
```
var dirList:seq[string]
dirList= @["east", "west", "south", "north"]

for dirIndex in 0..len(dirList)-1:
  echo dirList[dirIndex]
```
我们没有迭代元素, 而是通过列表的索引迭代.
这里我们使用了内建的 len() 函数获得序列长度, 
* 使用项和索引迭代
Nim中提供了另一非常方便的方法用来迭代序列:
```
for i, value in @[3, 4, 5]:
  echo("index: ", $i, ", value:", $value)
# --> index: 0, value:3
# --> index: 1, value:4
# --> index: 2, value:5
```
for语句可以使用一个或两个变量当用于一个序列的时候。当你使用一个变量的形式，这个变量将会持有序列提供的值。for语句从系统模型的items()迭代器循环这个结果。但是如果你使用两个变量的形式，第一个变量将保存索引位置，第二个变量将保存值，第二种形式for语句是从系统模型的pairs()迭代器循环结果。

### 6.6.2 其他
序列，“sequence”的简称，提供动态可扩展的存储.
有两种方式创建序列，使用@操作符以及使用newSeq[T](n: int)方法.一旦创建一个序列，可以使用add(item: T),delete(idx: int)修改它，序列的长度可以通过len: int得到，通过high: int特到最大索引值，通过low:int得到最小索引值。也可以使用标准items: T和pairs: tuple[i: int, v: T]迭代器。
```
var
  a = @[1, 2, 3]
  b = newSeq[int](3)

for i, v in a:
  b[i] = v*v

for i in 4..100:
  b.add(i * i)


b.delete(0)  # 删除第0个元素
b = a[0] & b  # 原始的b
```
### 6.6.3 不变性
虽然序列是动态分配的，但它们依然是不可变的.
```
let a = @[1, 2, 3]
a.add(4)
```
将编译失败，因为a不能被分配。然而，
```
var b = @[1, 2, 3]
b.add(4)
```
将会工作没有任何问题。如果想要，一个序列可以传递给一个方法使用var或者ref标记使它更改,所以:
```
proc foo(mySeq: var seq[int]) =
  mySeq[9] = 999

var thisSeq = newSeq[int](10)
foo(thisSeq)

assert thisSeq[9] == 999
```
