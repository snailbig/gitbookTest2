##6.10 元组

一个元组变量或对象变量是一个异构的存储容器。它们可以存储不同类型的数据，一个元组或对象定义了各种命名字段的类型，元组也定义了字段的顺序。元组作为异构存储类型意味着没有开销和很少可能抽象。我们可以通过构造器 () 来构造元组。在构造器中字段的顺序应该与元组定义时的顺序相同。如果不同的元组类型它们的字段是相同的名称和类型，并且有相同的命名顺序，那么可以认为它们是相同的。

    type 
      student = tuple[name: string, age: int]
      teacher = tuple[name: string, age: int]
    
    var
      x: student
      y: teacher
    x = (name: "yrs", age: 22)
    y = (name: "abc", age: 44)
    echo x
    x = y
    echo x


赋值运算符对于元组将会复制每一个部分，符号t.field是用于访问一个元组的字段。其他的方式是 t[i] 是访问第i个字段。这里的i需要是一个整型常量。

    type
      Person = tuple[name: string, age: int] # type representing a person:
                                             # a person consists of a name
                                             # and an age
    var
      person: Person
    person = (name: "Peter", age: 30)
    # the same, but less readable:
    person = ("Peter", 30)
    
    echo(person.name) # "Peter"
    echo(person.age)  # 30
    
    echo(person[0]) # "Peter"
    echo(person[1]) # 30
    
    # You don't need to declare tuples in a separate type section.
    var building: tuple[street: string, number: int]
    building = ("Rue del Percebe", 13)
    echo(building.street)
    
    # The following line does not compile, they are different tuples!
    #person = building
    # --> Error: type mismatch: got (tuple[street: string, number: int])
    # but expected 'Person'
    
    # The following works because the field names and types are the same.
    var teacher: tuple[name: string, age: int] = ("Mark", 42)
    person = teacher


虽然你不需要为了使用一个元组而声明一个类型，但是以不同的字段命名创建的元组将会被认为是不同的对象，尽管有相同的子段类型。

    var
      x: tuple[names: string, ages: int]
      y: tuple[name: string, age: int]
    x = (name: "yrs", age: 22)
    y = (name: "abc", age: 44)
    echo x
    x = y     #error  type mismatch
    echo x

在变量赋值时元组可以被打开（也只有这时），这个可以方便的直接赋给元组单独命名的字段变量，一个相关的例子是来自os模块的splitFile过程，它将同时返回目录、文件名和扩展名，对于元组拆箱工作，你不得不用圆括号括起要赋值的变量，不然将为所有的变量赋相同的值！例如：

    import os
    
    let
      path = "usr/local/nimc.html"
      (dir, name, ext) = splitFile(path)
      baddir, badname, badext = splitFile(path)
    echo dir            # outputs `usr/local`
    echo name           # outputs `nimc`
    echo ext            # outputs `.html`
    # All the following output the same line:
    # `(dir: usr/local, name: nimc, ext: .html)`
    echo baddir
    echo badname
    echo badext


元组拆箱也可以工作在var或let块外

    import os
    
    var
      path = "usr/local/nimc.html"
      dir, name, ext = ""
    
    (dir, name, ext) = splitFile(path)
    echo dir   
    echo name
    echo ext

元组也可以像声明一个对象一样定义，在type中可以通过缩进来代替 []：

    type
      Person = tuple   # type representing a person
        name: string   # a person consists of a name
        age: Natural   # and an age  Natural* = range[0..high(int)]
    
    var student:Person 
    student = (name: "yrs", age: Natural(22))
    echo(student)
#

    proc `^` (a, b: int): int =
      result = 1
      for i in 1..b: result = result * a
    
    var m = (0, 5)
    var n = (56, 3)
    
    m = (n[0] + m[1], m[1] ^ n[1])
    
    echo m[0], ", ", m[1] 

#

    type
      Item[K,V] = tuple
        key: K
        value: V
    
    var q = newseq[Item[int,int]](1) 
    q[0].key = 1
    q[0].value = 2
    let (x,y) = q[0]
    echo (x,y)

#

    proc test(): int64 =
      return 0xdeadbeef.int64
    
    const items = [
      (var1: test(), var2: 100'u32),
      (var1: test(), var2: 192'u32)
    ]
    
    echo items[0]
    echo items[1]
    
    
    
# 任真
***
## 6.10 元组
***
元组类型的变量是一个异构的存储容器。一个元组定义了一个类型的各种命名域。一个元组也定义了域的顺序。元组意味着没有开销的异构存储类型以及一些抽象的可能性。一个元组类型定义不同的命名域以及域的顺序。构造器（）可以用来构造元组。构造器中的域的顺序必须和元组中定义的域的顺序一致。不同的元组类型是等价的，如果它们指定的域有相同的类型，并且相同的名字有相同的顺序。

元组的赋值操作复制每个元素。符号t.field用于访问一个元组的域。另一个符号是t[i]用来访问第i个域。这里i需要是一个整型常数。
```
type
  Person = tuple[name: string, age: int] #  类型代表一个人，包含name域和age域
                                         
                                   
var
  person: Person

person = (name: "Peter", age: 30)
#下面这样也是等价的，但是缺少可读性
person = ("Peter", 30)

echo(person.name) # "Peter"
echo(person.age)  # 30

echo(person[0]) # "Peter"
echo(person[1]) # 3

#你不需要在一个单独的类型部分声明元组
var building: tuple[street: string, number: int]
building = ("Rue del Percebe", 13)
echo(building.street)

#下面的行不能编译，它们是不同的元组
#person = building
# --> Error: type mismatch: got (tuple[street: string, number: int])
#     but expected 'Person'

# The following works because the field names and types are the same.     下面的有效因为元组的域名称和类型是相同的
var teacher: tuple[name: string, age: int] = ("Mark", 42)
person = teacher
echo person.name
echo person.age
```
你不需要为元组声明一个类型来使用它，使用不同的域名称创建元组将被认为不同的对象，尽管有相同的域类型。注:难道这句话的意思是说”创建元组时可以不显示的说明域的类型吗??”例如:
```
type
  Person = tuple[name, age]
                                 
```
这样是错误的???

在变量赋值期间可以元组拆箱(也只有在这时才可以)。这可以方便的直接分配元组域为了单独命名变量。这方面的一个例子是来自OS模块，同时返回目录，名字和一个路径的拓展名的splitfile过程。为了使元组拆包工作你必须使用括号包裹你想要拆包分配的值，否则你将给所有的单独变量分配相同的值.例如:
```
import os

let
  path = "usr/local/nimc.html"
  (dir, name, ext) = splitFile(path)         #这里使用括号
  baddir, badname, badext = splitFile(path)

echo dir      # outputs `usr/local`
echo name     # outputs `nimc`
echo ext      # outputs `.html`

#下面将输出相同的行:　
# `(dir: usr/local, name: nimc, ext: .html)`
echo baddir
echo badname
echo badext
```
splitFile()来自OS模块.过程原型为:
```
proc splitFile(path: string): tuple[dir, name, ext: string] {.noSideEffect, gcsafe,
    extern: "nos$1", raises: [], tags: [].}
```
将一个文件名展开为(dir, filename, extension),dir在DirSep不结束,extension包括引导点.
例如:
```
var (dir, name, ext) = splitFile("usr/local/nimc.html")
assert dir == "usr/local"
assert name == "nimc"
assert ext == ".html"
```
如果路径没有扩展名,ext是空字符串。如果路径没有目录部分，dir是空字符串，如果路径没有文件名部分，name和ext为空字符串.

元组拆箱只作用在var或者let块。下面的代码不能编译:  
```
import os

var
  path = "usr/local/nimc.html"
  dir, name, ext = ""

(dir, name, ext) = splitFile(path)
```
注,关于这个代码不同的编译器出现了不同的结果:
* 0.12.1版本下，编译运行是正确的.
* 0.11.2版本下，出现了Error: '(dir, name, ext)' cannot be assigned to

### 6.10.2 元组和对象类型
元组或者对象类型的变量是一个异构的存储容器。一个元组或者对象定义了一个类型的各种命名域。一个元组也定义了域的顺序。元组意味着没有开销的异构存储类型以及一些抽象的可能性。构造器()可用于构造元组。构造器中的域的顺序必须匹配元组定义的域的顺序。不同的元组类型如果它们指定的相同类型的相同域有相同的顺序则它们是等价的。域的名字也必须相同。

对于元组的赋值操作复制每一个元素。对于对象的默认赋值操作复制每一个元素。为对象重载赋值运算符是不可能的，但是这将在未来的编译器版本中改变。
```
type
  Person = tuple[name: string, age: int] #  类型代表一个人，包含name域和age域
                                         
                                   
var
  person: Person

person = (name: "Peter", age: 30)
#下面这样也是等价的，但是缺少可读性
person = ("Peter", 30)
```
为了更好的访问性能实行字段对齐.对齐方式与c编译器的处理方式兼容.
为了与object对象声明的一致性，在一个type部分可以使用缩进而不是[]定义元组：
```
type
  Person = tuple   # type representing a person
    name: string   # a person consists of a name
    age: natural   # and an age
```
对象提供了很多元组没有的特性。对象提供了继承和信息隐藏。对象可以在运行时访问它们的类型，以便of操作符可用于确定对象的类型。
```
type
  Person {.inheritable.} = object
    name*: string      #   *意味着从其他模块可以访问到`name`
    age: int           #   没有*意味着该域是隐藏的
  
  Student = ref object of Person # a student is a person
    id: int                  # with an id field

var
  student: Student
  person: Person
assert(student of Student)       # is true
assert(student of Person)        # also true
```
从外部定义的模块可见的对象域必须用*标记。与元组不同的是，不同的对象类型是绝不等价的。没有祖先的对象是隐式的终点，所以没有隐藏的类型域。你可以使用inheritable编译指示引入除了system.RootObj之外新的根对象。

### 6.10.3 对象
在Nim中，对象就像来自C语言家族的(structs)结构体,它定义了一组域.它们默认被垃圾收集器跟踪，所以当分配对象之后，没有必要显式的(free)释放它们
```
type
  Animal* = object
    name*, species*: string
    age: int

proc sleep*(a: var Animal) =
  a.age += 1

proc dead*(a: Animal): bool =
  result = a.age > 20

var carl: Animal

carl = Animal(name : "Carl",
              species : "L. glama",
              age : 12)

let joe = Animal(name : "Joe",
                 species : "H. sapiens",
                 age : 23)

assert(not carl.dead)

for i in 0..10:
  carl.sleep()

assert carl.dead
```
通常,对象类型在type部分定义. 它们可以被导出，单个字段也可以被导出.字段可以不违反封装的安全导出，因为在它们之间的调用语法是等价的。

最初，carl在栈上创建并初始化为0,所以它的值是[name = nil, species = nil, age = 0]。它是可变的，所以意味着carl的内容可以改变.这也意味着carl可以传递给一个要求可变参数的过程，如sleep(),它可以修改它的值。

joe也在栈上创建，但是它的内容是不可变的，不能被改变。试图这样做，通过joe.age = 57，将在编译时出现错误而失败.如，在上面的代码中，我们用joe调用sleep()过程,将会出现错误:
```
joe.sleep()         #Error
```
```
type
  Animal* = object
    name*, species*: string
    age: int

proc sleep*(a: var Animal) =
  a.age += 1

proc dead*(a: Animal): bool =
  result = a.age > 20


let mittens: ref Animal = new(Animal)

mittens.name = "Mittens"
mittens.species = "P. leo"
mittens.age = 6

mittens.sleep()    #Error
```
mittens是一个对象的引用在堆上分配。mittens的值不能改变，所以mittens不可能指向其他的任何东西，但是mittens指向的值可以和从默认的初始化值0改变。它的易变性也意味着mittens可以传递给一个要求可变参数的函数(过程)。

你可能问是否有更简洁的方式初始化引用类型，如果你给引用类型一个名字这就是比较简洁的方式:例如:
```
type
  PAnimal* = ref Animal

let spot = PAnimal(name: "Spot",
                   species: "C. lupus",
                   age: 1)
```
在多数情况下，仅想要一个引用类型的对象，通过声明它为ref object为一个引用类型的对象。
```
type
  Thing* = ref object
    positionX*, positionY*: int


var tValue=Thing(positionX:3,positionY:4)
```

### 6.10.4 对象构造
对象也可以使用一个对象构造表达式来创建，对象构造表达式有这样的语法T(fieldA: valueA, fieldB: valueB, ...) ，T是一个object类型或者是一个ref object类型：

例如:
```
var student = Student(name: "Anton", age: 5, id: 3)
```
对于一个ref object类型隐式的调用system.new。
