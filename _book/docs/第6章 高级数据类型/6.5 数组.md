##6.5 数组
数组是一个固定长度的容器，数组的每一个元素有相同的类型，数组的索引可以是任何的序数类型。
我们能够通过 [] 来构造数组：

    type
      IntArray = array[0..5, int]     # an array that is indexed with 0..5
    var
      x: IntArray
    x = [1, 2, 3, 4, 5, 6]
    for i in low(x)..high(x):     #low(x),high(x)返回数组的最下索引和最大索引
      echo(x[i])

符号x[i]是用来访问数组x的第i个元素，数组访问总是有边界检查的（在编译时或运行时）。这些检查能够通过编译指示来关闭掉或者通过编译命令 --bound_checks:off来关闭边界检查。

    {.boundChecks:off.} 
    #或者使用编译命令运行：   nim c --bound_checks:off -r TboundChecksArray
    var x = [1,2,3]
    echo repr(x)
    echo x[9]


像Nim其它的类型，数组是一个值类型，赋值运算符复制数组的整个内容。

内建的len过程返回数组的长度，low(a)返回数组a的最小索引，high(a)返回最大索引。

    type
      Direction = enum
        north, east, south, west
      BlinkLights = enum
        off, on, slowBlink, mediumBlink, fastBlink
      LevelSetting = array[north..west, BlinkLights]
    var
      level: LevelSetting
    echo repr(level) #数组初始默认值是 off ,也就是类型 BlinkLights的第一个枚举项
    level[north] = on
    level[south] = slowBlink
    level[east] = fastBlink
    echo repr(level)  # --> [on, fastBlink, slowBlink, off]
    echo low(level)   # --> north
    echo len(level)   # --> 4
    echo high(level)  # --> west


在其它的语言中，嵌套数组（多维数组）的语法是附加更多的括号，因为通常每个维度是受限于其它维度的相同的索引类型。在Nim中你可以不同的维度使用不同类型的索引（序数类型：整型、字符型、布尔类型、连续的枚举型等），因此嵌套语法是略微有些不同的，请看下面这个例子：

    type
      Direction = enum
        north, east, south, west
      BlinkLights = enum
        off, on, slowBlink, mediumBlink, fastBlink
      LevelSetting = array[north..west, BlinkLights]
      LightTower = array[1..10, LevelSetting]
    var
      tower: LightTower
    tower[1][north] = slowBlink
    tower[1][east] = mediumBlink
    echo len(tower) # --> 10
    echo len(tower[1])  # --> 4
    echo repr(tower)# --> [[slowBlink, mediumBlink, ...more output..
    # The following lines don't compile due to type mismatch errors
    #tower[north][east] = on
    #tower[0][1] = on

注意，内建的过程len仅仅返回数组的一个维度的长度，在这个例子中定义了一个二维数组类型LightTower，它的一个维度的索引是枚举类型，另一个维度的索引是整型。也有其他的定义类型LightTower的方式，并且更能展示它的嵌套性质的放式是省略前面定义的LevelSetting类型，直接在里面嵌套写出这个类型。

    type LightTower = array[1..10, array[north..west, BlinkLights]]

#
    type
      TEnum = enum
        eA, eB, eC, eD, eE, eF
    
    const
      myMapping: array[TEnum, array[0..1, int]] = [
        eA: [1, 2],
        eB: [3, 4],
        [5, 6],
        eD: [0: 8, 1: 9],
        eE: [0: 8, 9],
        eF: [2, 1: 9]
      ]
    
    echo myMapping[eC][1]

#
二维数组：

    type
      ODArray = array[3,int]
      TDArray = array[3,ODArray]
    
    var myarray: TDArray = [[1,2,3],[4,5,6],[7,8,9]]
    
    echo myarray[0][0]

#
三维数组：

    type
      ODArray = array[2,int]
      TDArray = array[2,ODArray]
      ThDArray = array[2,TDArray]
    
    var myarray2:ThDArray = [[[1,2],[3,4]],[[5,6],[7,8]]]
    echo myarray2[0][0][0]


我们经常使用数组从索引0开始，因此这里有一个快捷的语法去指定从0到指定的索引减1的范围：

    type
      IntArray = array[0..5, int] # an array that is indexed with 0..5
      QuickArray = array[6, int]  # an array that is indexed with 0..5
    var
      x: IntArray
      y: QuickArray
    x = [1, 2, 3, 4, 5, 6]
    y = x
    for i in low(x)..high(x):
      echo(x[i], y[i])

#
    type
      TMyArray = array[0..2, int]
      TMyRecord = tuple[x, y: int]#定义了一个元组
    
    proc sum(a: TMyarray): int =
      result = 0
      var i = 0
      while i < len(a):
        inc(result, a[i])
        inc(i)
    
    proc sum(a: openarray[int]): int =
      result = 0
      var i = 0
      while i < len(a):
        inc(result, a[i])
        inc(i)
    
    proc getPos(r: TMyRecord): int =
      result = r.x + r.y
    
    write(stdout, sum([1, 2, 3, 4]))
    write(stdout, sum([]))
    write(stdout, getPos( (x: 5, y: 7) ))

#
    type
      TMyArray = array[0..2, int]
    
      TObj = object
        arr: TMyarray
    
    proc mul(a, b: TMyarray): TMyArray =
      result = a
      for i in 0..len(a)-1:
        result[i] = a[i] * b[i]
    
    var
      x, y: TMyArray
      o: TObj
    
    proc varArr1(x: var TMyArray): var TMyArray = x
    proc varArr2(x: var TObj): var TMyArray = x.arr
    
    x = [ 4, 5, 6 ]
    echo repr(varArr1(x))
    
    y = x
    echo repr(mul(x, y))
    
    o.arr = mul(x, y)
    echo repr(varArr2(o))

#

    proc `+`*[R, T] (v1, v2: array[R, T]): array[R, T] =
      for i in low(v1)..high(v1):
        result[i] = v1[i] + v2[i]
    
    var
      v1: array[0..1, float] = [3.0, 1.2]  #array[0..2, float] = [3.0, 1.2, 1.0] is error 
      v2: array[0..1, float] = [2.0, 1.0]
      v3 = v1 + v2
    
    echo repr(v3)


#
    var 
      a = [1,2,3]
      n: int
    echo a[^3]       #相当与   echo a[a.len - 3]

#
```
type
  Matrix[W, H: static[int]] =
    array[1..W, array[1..H, int]]

let mat1: Matrix[2, 2] = [[1, 0],
                          [0, 1]]
let mat2: Matrix[2, 2] = [[0, 1],
                          [1, 0]]

proc `+`[W, H](a, b: Matrix[W, H]):
               Matrix[W, H] =
  for i in 1..high(a):
    for j in 1..high(a[0]):
      result[i][j] = a[i][j] + b[i][j]


echo repr(mat1 + mat2)
```


# 任真
***
## 结构化类型
一个结构化类型的变量可以同时容纳多个值。结构化类型可以嵌套到无限的级别。数组，序列，元组，对象和集合都属于结构类型。
## 6.5 数组
***
数组是一个同性质的类型，意味着数组中的每一个元素有相同的类型。数组通常有一个在编译时确定的固定（除了开放数组）的长度。它的索引类型可以是任何序数类型。参数A可能是一个开放数组，在这种情况下它通过从0到len(A)-1的整数索引。可以通过数组构造器[]构造一个数组表达式.

数组是一种简单的固定长度的容器。数组中的每个元素具有相同的类型。数组的索引类型可以是任何序数类型。
数组可以通过[]构造：
```
type
  IntArray = array[0..5, int] #一个索引值为0..5的数组
var
  x: IntArray
x = [1, 2, 3, 4, 5, 6]
for i in low(x)..high(x):
  echo(x[i])
```
标记x[i]是用来访问x的第i个元素。数组访问总是进行边界检查（在编译或者运行时）。这些检查可以禁用通过编译指示或使用命令行开关——bound_checks：off 调用编译器

数组总是进行边界检查（在编译时或者运行时）。这些检查可以通过编译指示或者调用编译器的boundChecks:off命令行参数开关禁用。
* 通过使用编译指示:
```
{.boundChecks:off.}  #该编译指示的作用为关闭数组的边界检查

type
  IntArray = array[0..5, int] #一个索引值为0..5的数组
var
  x: IntArray
x = [1, 2, 3, 4, 5, 6]
for i in low(x)..high(x):
  echo(x[i])

x[6]=7   #下标越界,如果数组的边界检查关闭，则该句代码运行时不会出错
```
* 通过编译命令行开关:
```
#{.boundChecks:off.}  #该编译指示的作用为关闭数组的边界检查

type
  IntArray = array[0..5, int] #一个索引值为0..5的数组
var
  x: IntArray
x = [1, 2, 3, 4, 5, 6]
for i in low(x)..high(x):
  echo(x[i])

x[6]=7   #下标越界,如果数组的边界检查关闭，则该句代码运行时不会出错
```
数组边界检查的开关默认是打开的。这里我们仅把程序开始的那个编译指示注释掉#{.boundChecks:off.}。方便运行，我们假设将上面的程序保存为arrayTest2.nim然后我们使用下面的命令运行程序:
```
nim c --boundChecks:off arrayTest2.nim  
```
在编译是加上--boundChecks:off 编译选项，这样编译器也不会进行数组的边界检查.

数组是值类型，像其他nim类型，赋值操作将复制整个数组内容.
```
type
  ArrType=array[0..3,int]

var
  aValue:ArrType=[1,2,3,4]
  bValue:ArrType

bValue=aValue   #赋值操作将复制整个aValue数组的内容到bValue数组

for i in 0..len(bValue)-1:
  echo bValue[i]
```
内置的len过程返回数组的长度。low(a)返回数组a的最小有效下标，high(a)返回数组a的最大有效下标。
```
type
  Direction = enum
    north, east, south, west
  BlinkLights = enum
    off, on, slowBlink, mediumBlink, fastBlink
  LevelSetting = array[north..west, BlinkLights]
var
  level: LevelSetting

echo repr(level)   #  -->[off, off, off, off]  用BlinkLights第一个值初始化数组


level[north] = on
level[south] = slowBlink
level[east] = fastBlink
echo repr(level)  # --> [on, fastBlink, slowBlink, off]
echo low(level)   # --> north
echo len(level)   # --> 4
echo high(level)  # --> west
```
这个例子中的索引类型和元素类型都是枚举类型，数组的索引类型必须是序数类型。
### 6.5.2 多维数组
在其他语言中对于嵌套的数组(多维数组)语法是附加更多的括号，因为通常每个维度仅限于相同的索引类型。在Nim中你可以有不同索引类型的不同维度，所以潜逃语法是相当简单的。前面的例子中，level被定义为被另一枚举类型索引的枚举数组，在此基础上，我们可以添加下列代码来增加一个light tower类型通过它们的整型索引高层访问:
```
type
  Direction = enum
    north, east, south, west
  BlinkLights = enum
    off, on, slowBlink, mediumBlink, fastBlink
  LevelSetting = array[north..west, BlinkLights]
var
  level: LevelSetting


type
  LightTower = array[1..10, LevelSetting]

var
  tower: LightTower

echo repr(tower)

tower[1][north] = slowBlink
tower[1][east] = mediumBlink
echo len(tower)     # --> 10
echo len(tower[1])  # --> 4
echo repr(tower)    # --> [[slowBlink, mediumBlink, ...more output..
# The following lines don't compile due to type mismatch errors
#tower[north][east] = on
#tower[0][1] = on
```
注意：内置的len过程仅返回数组的第一维的长度。另一种方式定义LightTower为了表现出更好的嵌套性质，将省略前面的LevelSetting类型的定义，将它直接嵌套写入作为第一维度的类型：
```
type
  LightTower = array[1..10, array[north..west, BlinkLights]]
```
我们来举一些简单多维数组的例子:
```
type
  ArrOneType=array[0..3,int]            #一维数组，数组索引类型为整型，元素也为整型
  ArrTwoType=array[0..2,ArrOneType]     #二维数组，索引类型为整型，元素类型为ArrOneType类型


var
  aOneValue:ArrOneType
  aTwoValue:ArrTwoType=[[1,2,3,4],[5,6,7,8],[9,10,11,12]]  #初始化二维数组

echo repr(aTwoValue)

echo len(aTwoValue)     #二维数组的长度
echo len(aTwoValue[0])  #二维数组中一组数组的长度

for i in 0..len(aTwoValue)-1:            #遍历二维数组的另一种方法
  for j in 0..len(aTwoValue[i])-1:
     echo aTwoValue[i][j]
```
同理，我们可以这样定义三维数组，以及多维数组:
```
type
  ArrOneType=array[0..3,int]            #一维数组，数组索引类型为整型，元素也为整型
  ArrTwoType=array[0..2,ArrOneType]     #二维数组，索引类型为整型，元素类型为ArrOneType类型
  ArrThreeType=array[0..1,ArrTwoType]   #三维数组
```
数组索引从0开始是相当常见的，指定一个从0到指定的下标值减一的范围是一个简便快捷的语法：
```
type
  IntArray = array[0..5, int] # 索引值为0..5的数组
  QuickArray = array[6, int]  # 索引值为0..5的数组

var
  x: IntArray
  y: QuickArray
x = [1, 2, 3, 4, 5, 6]
y = x
for i in low(x)..high(x):
  echo x[i]," ",y[i]
```
### 6.5.3 其他
Nim中的数组类似于典型的c数组,它们的大小在编译时确定，并且不能在运行时给出或改变.
```
type
  ThreeStringAddress = array[3, string]
let names: ThreeStringAddress = ["Jasmine", "Ktisztina", "Kristof"]
let addresses: ThreeStringAddress = ["101 Betburweg", "66 Bellion Drive", "194 Laarderweg"]
```
数组的大小编码在它的类型中，不能意外丢弃。因此，一个带有数组长度变量的过程必须在它的类型参数中编码长度.
```
proc zip[I, T](a, b: array[I, T]):
               array[I, tuple[a, b: T]] =
  for i in low(a)..high(a):
    result[i] = (a[i], b[i])

let nameAndAddresses = names.zip(addresses)
```
整个测试例子:
```
type
  ThreeStringAddress = array[3, string]

let names: ThreeStringAddress = ["Jasmine", "Ktisztina", "Kristof"]
let addresses: ThreeStringAddress = ["101 Betburweg", "66 Bellion Drive", "194 Laarderweg"]

proc zip[I, T](a, b: array[I, T]):
               array[I, tuple[a, b: T]] =
  for i in low(a)..high(a):
    result[i] = (a[i], b[i])

let nameAndAddresses = names.zip(addresses)

for i in 0..len(nameAndAddresses)-1:
  echo nameAndAddresses[i]
```
可选择的索引数组的方法也是允许的，第一个类型参数实际上是一个范围(仅一个值，如上，对于0..N-1是语法糖)。使用序数类型去索引一个数组也是可能的，有效地创建一个查找表:
```
type
  PartsOfSpeech {.pure.} = enum
    Pronoun, Verb, Article, Adjective, Noun, Adverb


let partOfSpeechExamples: array[PartsOfSpeech, string] = [
  "he", "reads", "the", "green", "book", "slowly"
]

怎样使用这个查找表???
```
```
type
  Matrix[W, H: static[int]] =
    array[1..W, array[1..H, int]]

let mat1: Matrix[2, 2] = [[1, 0],
                          [0, 1]]
let mat2: Matrix[2, 2] = [[0, 1],
                          [1, 0]]

proc `+`[W, H](a, b: Matrix[W, H]):
               Matrix[W, H] =
  for i in 1..high(a):
    for j in 1..high(a[0]):
      result[i][j] = a[i][j] + b[i][j]

# See `$` from the procs section

echo repr(mat1+mat2)    
```
