##模块

Nim支持将一个程序拆分成模块的概念。每一个模块是一个Nim文件。模块能够隐藏信息，并且可以单独编译。一个模块可以通过import语句访问其他模块中的符号，仅仅是被 * 标记的顶层符号才能被其他模块使用。一个有效的模块名称仅仅是一个有效的Nim标识符（因此它的文件名是identifier.nim)。

```
# Module A
var
  x*, y: int

proc `*` *(a, b: seq[int]): seq[int] =
  # allocate a new sequence:
  newSeq(result, len(a))
  # multiply two int sequences:
  for i in 0..len(a)-1: result[i] = a[i] * b[i]

when isMainModule:
  # test the new ``*`` operator for sequences:
  assert(@[1, 2, 3] * @[1, 2, 3] == @[1, 4, 9])
 
```

以上模块输出x和*，但是没有y。

一个模块的顶层语句当程序开始时将执行，这个可以用来初始化复杂的数据结构。

每一个模块都有一个特别的魔法常量isMainModule，如果这个模块作为一个main文件被编译，那么它是true。这对于嵌入模块测试是非常有用的，如上面的例子所示。

模块相互依赖是可能的，但是强烈不推荐，因为没有其他模块，这个模块将不能被重用。

编译模块的算法是：


1. 像往常一样编译整个模块，接着是递归import语句。


1. 如果有一个循环仅仅导入已经解析的符号（它被输出）；如果出现一个未知的标识符，那么终止。


这是最好的一个例子：
```
# Module mA
type
  T1* = int  # Module mA exports the type ``T1``
import mB     # the compiler starts parsing mB

proc main() =
  var i = p(3) # works because mB has been parsed completely here

main()
```
#

```
# Module mB
import mA  # mA is not parsed here! Only the already known symbols
          # of mA are imported.

proc p*(x: mA.T1): mA.T1 =
  # this works because the compiler has already
  # added T1 to mA's interface symbol table
  result = x + 1
```

一个模块中的符号A可以用module.symbol语法来限制调用。如果这个符号是有歧义的，那么不得不使用这个限制了。如果一个符号被定义在两个（或更多）不同的模块内，并且这两个模块都被第三个模块导入，那么这个符号是歧义的。
```
# Module A
var x*: string
```
#
```
# Module B
var x*: int
```
#
```
# Module C
import A, B         #注意模块名，在windows下，文件名首字母被认为是小写
write(stdout, x) # error: x is ambiguous
write(stdout, A.x) # no error: qualifier used

var x = 4
write(stdout, x) # not ambiguous: uses the module C's x
```

但是这个规则不能用于过程或者迭代器。这里应用重载规则：
```
# Module A
proc x*(a: int): string = $a
```
#
```
# Module B
proc x*(a: string): string = $a
```
#

```
# Module C
import A, B
write(stdout, x(3))   # no error: A.x is called
write(stdout, x(""))  # no error: B.x is called

proc x*(a: int): string = $(a+1)
write(stdout, x(3))   # no error: C.x is called

```

递归导入
```
#module mrecmod
import trecmod
```
#
```
#module trecmod
# recursive module
import mrecmod
```
##Import expect语句

正常的import语句将导入所有的导出符号。可以通过except限定符限制要排除在外的符号，这样在这个模块中就不能使用该符号了。
```
import strutils except `%`, toUpper

# doesn't work then:
echo "$1" % "abc".toUpper
```

它不会检查except列表中的符号是否真的从这个模块输出的。这个特性允许编译器处理这个模块没有导出这些标识符的旧版本。
```
#Module a
var 
  name* = "yrs"
  age* = 22
```
#
```
import a except z    #没有检查z
echo "Hi ",name,", you are ",age, " years"
```


##From import 语句

我们已经看到了使用简单的import语句能够导入一个模块所有的输出符号，另一个方式是通过from import语句仅仅导入列出的符号：
```
from strutils import `%`

echo "$1" % "abc"
# always possible: full qualification:
echo strutils.replace("abc", "a", "z")
```

From语句可以对符号进行强制的命名空间限定，从而使符号可用，但是需要被限定的使用。
```
#Module  mymodule
proc x*() = 
  echo "this is x proc"

proc y*() = 
  echo "this is y proc"
  
proc z*() = 
  echo "this is z proc"
```
#
```
from mymodule import x, y, z

x()           # use x without any qualification
```
#
```
from mymodule import nil

mymodule.x()  # must qualify x with the module name as prefix

x()           # using x here without qualification is a compile error
```

由于模块名通常都是很长的描述，你可以为模块名定义一个短的别名，从而使用时比较方便：
```
from mymodule as m import nil

m.x()         # m is aliasing mymodule
```




##Include语句
Include语句与导入一个模块有一些根本的不同：它仅仅包含一个文件的内容。Include语句是对于把一个大模块分成几个文件是非常有用的。
```
#Module fileA
type
  Person = object
    name: string
    age: int
```
#
```
#Module fileB
proc print(x: Person) = 
  echo "Hi ",x.name
```
#
```
include fileA, fileB

var student: Person
student.name = "yrs"
student.age = 22
student.print()
```

从上面的例子可以看出，include就像是把文件fileA和fileB都包括在最后一个测试文件内，它们像是一个模块，文件fileA和fileB中的标识符不用使用 * 进行标记就能在最后一个文件中使用。而import语句导入模块，只导人被 * 标记的标识符。


```
# Test recursive includes
#module trecincb
include trecincb

echo "trecinb"     #Error: recursive dependency: 'trecincb.nim'
```
#
```
# Test recursive includes
#module trecina
include trecincb

echo "trecina"
```


##模块别名

我们可以通过关键字as来为模块定义一个别名：
```
import strutils as su, sequtils as qu

echo su.format("$1", "lalelu")
```

原始的模块名称将不能使用。可以使用path/to/modul或path.to.module或"path/to/module"来指定在子目录中的一个模块。

    import lib.pure.strutils, lib/pure/os, "lib/pure/times"

注意模块的名字仍然是strutils，而不是lib.pure.strutils，因此我们不能做：
```
import lib.pure.strutils
echo lib.pure.strutils
```

同样的下面的也是没有意义的，因为它的名字早就是strutils。

    import lib.pure.strutils as strutils

    



##Export语句

Export语句可以用于符号转发，这样客户模块就不用导入一个模块的依赖：
```
# module B
type MyObject* = object
```
#
```
# module A
import B
export B.MyObject

proc `$`*(x: MyObject): string = "my object"
```
#
```
# module C
import A

# B.MyObject has been imported implicitly here:
var x: MyObject
echo($x)
```

##注意路径

在与模块相关的语句中，如果模块路径的任何部分以一个数字开始，那么你不得不用双引号把路径括起来。

    import "gfx/3d/somemodule"

 
#
```
# module mexportb
type TMyObject* = object

const xyz* = 13

proc q*(x: int): int = 6
proc q*(x: string): string = "8"
```
#
```
# module mexporta
import mexportb
export mexportb.TMyObject, mexportb.xyz

export mexportb.q

proc `$`*(x: TMyObject): string = "my object"
```
#
```
import mexporta

# mexportb.TMyObject has been imported implicitly here:
var x: TMyObject
echo($x, q(0), q"0")   
```
#
***

#
```
# module mexport2b
proc printXyz*() = echo "xyz"

proc foo*(x: int) = echo "B.foo"
```
#
```

# module mexport2a
import mexport2b
export mexport2b #若没有该语句，第三个文件中 foo(3)将会出错
proc printAbc*() = echo "abc"

proc foo*() = echo "A.foo"
```
#
```
import mexport2a

proc main() =
  echo "Import Test, two lines should follow. One with abc and one with xyz."
  printAbc()
  printXyz()

main()
foo(3)
```
***
#
```
# module mopaque
type
  TLexer* {.final.} = object
    line*: int
    filename*: string
    buffer: cstring

proc noProcVar*(): int = 18
```
#
```
import mopaque

var
  L: TLexer

L.filename = "ha"
L.line = 34
L.buffer[0] = '\0' #ERROR_MSG undeclared field: 'buffer'
```